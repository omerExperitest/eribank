import com.experitet.gradle.ZipConfig

buildscript {
    repositories {
        maven {
            url "${artifactory_url}/libs-release"

            credentials {
                username "${artifactory_user}"
                password "${artifactory_password}"
            }
        }

        dependencies {
            classpath 'com.experitest.gradle.plugin:zipPublishing:0.0.4'
        }
    }
}

apply plugin: 'com.experitest.publish.zip'
apply plugin: 'com.android.application'

ext.defaultBuildVersion = "999"
ext.productVersion = "12.4"
ext.buildNumber = System.getenv("BUILD_NUMBER") ?: defaultBuildVersion
ext.shouldPublish = ext.buildNumber != defaultBuildVersion

group = 'com.experitest.android'
version = productVersion + '-SNAPSHOT'

android {
    compileSdkVersion 23
    defaultConfig {
        applicationId "com.experitest.ExperiBank"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode = Integer.valueOf(getExternalValue('build.number','1'))
        versionName = '1.' + (getExternalValue('build.number','1'))
    }
    signingConfigs {

        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def newName = "eribank.apk"
                    outputFileName = newName
                }
            }
        }
    }



    productFlavors {
    }

    lintOptions {
        abortOnError false
    }
}

def getExternalValue(valueName, defaultValue) {
    try {
        return teamcity[valueName]
    } catch (exception) {
        if (System.getProperty(valueName) == null) {
            return defaultValue
        } else {
            return System.getProperty(valueName)
        }

    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    testCompile 'junit:junit:4.12'
}

publishing.repositories {
    maven {
        url = "${artifactory_url}" + "/libs-snapshot-local"
        credentials {
            username "${artifactory_user}"
            password "${artifactory_password}"
        }
    }
}

publishing.publications {
    eribank(MavenPublication) {
        artifactId 'eribank'
    }
}

publishEribankPublicationToMavenRepository.publishConfig {
    config = new ZipConfig(file("${buildDir}/outputs/apk/release/eribank.apk"), new File(buildDir, "publishing"))
}

publishEribankPublicationToMavenRepository.dependsOn 'assembleRelease'